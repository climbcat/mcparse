/*******************************************************************************
*
* McXtrace, xray-tracing package
*         Copyright (C) 1997-2008, All rights reserved
*         Risoe National Laboratory, Roskilde, Denmark
*         Institut Laue Langevin, Grenoble, France
*
* Instrument: templateSasView
*
* %Identification
* Written by: Torben Nielsen 
* Modified by: Erik b knudsen
* Date: Jan 2017.
* Origin: DTU Physics / ESS DMSC
* Release: 1.5
* 
* %INSTRUMENT_SITE: Templates
*
* Test instrument for the SasView_model component. No optics.
* etc. 
*
* Example: --D1=SASmodel_index=1 model_index=1 par1=4 par2=1 par3=40 par4=20 par5=400
* Example: --D1=SASmodel_index=2 model_index=2 par1=4 par2=1 par3=40 par4=20 par5=400 par6=60 par7=60
* Example: --D1=SASmodel_index=3 model_index=3 par1=220 par2=0.06 par3=40 par4=4 par5=1
* Example: --D1=SASmodel_index=4 model_index=4 par1=4 parX=2 par3=20 par4=20 par5=400 par6=60 par7=60
* Example: --D1=SASmodel_index=5 model_index=5 par1=4 parX=1 par2=20 par3=20 par4=400
* Example: --D1=SASmodel_index=6 model_index=6 par1=4 par2=1 par3=20 par4=20 par5=400 par6=0 par7=0
* Example: --D1=SASmodel_index=6 model_index=6 par1=4 par2=1 par3=20 par4=20 par5=400 par6=60 par7=60
* Example: --D1=SASmodel_index=10 model_index=10 par1=4 par2=1 par3=20 par4=400 par5=60 par6=60
* Example: --D1=SASmodel_index=21 model_index=21 par1=60
* Example: --D1=SASmodel_index=47 model_index=47 par1=4 parX=2 par3=35 par4=75 par5=400
* Example: --D1=SASmodel_index=48 model_index=48 par1=4 parX=1 par2=35 par3=75 par4=400 par5=60 par6=60 par7=60
* Example: --D1=SASmodel_index=55 model_index=55 par1=50.0 par2=0.2 par3=0.05 par4=0.2
*
* %Description
* Very simple test instrument for the SasView_model component
*
* %Parameters
* INPUT PARAMETERS:
* lambda:    Mean wavelength of x-rays     [AA]
* dlambda:   Wavelength spread of x-rays    [AA]
*
* %Link
* %End
*******************************************************************************/
DEFINE INSTRUMENT templateSasView(lambda=1.54, dlambda=0.05, model_index=10, par1=0, par2=0, par3=0, par4=0, par5=0, par6=0, par7=0, par8=0)

DECLARE %{
  double modelpars[8];
%}

INITIALIZE %{
  modelpars[0]=par1;
  modelpars[1]=par2;
  modelpars[2]=par3;
  modelpars[3]=par4;
  modelpars[4]=par5;
  modelpars[5]=par6;
  modelpars[6]=par7;
  modelpars[7]=par8;
%}

TRACE

COMPONENT a1 = Progress_bar()
  AT (0,0,0) ABSOLUTE

COMPONENT arm = Arm(
    )
  AT (0, 0, 0) ABSOLUTE

COMPONENT source = Source_flat (
    radius = 0.0005, dist = 3, focus_xw = .5e-3, focus_yh = .5e-3,
    lambda0 = lambda, dlambda = dlambda, flux = 1e8)
  AT (0, 0, 0) RELATIVE arm

COMPONENT coll1 = Slit(
    radius = 0.0005)
  AT (0, 0, 3) RELATIVE arm

COMPONENT coll2 = Slit(
    radius = 0.0005)
  AT (0, 0, 6) RELATIVE arm

// Example sphere
SPLIT COMPONENT sample = SasView_model(model_index=model_index, model_scale=1, model_pars=modelpars,
model_abs=0.0,
  xwidth=0.01, yheight=0.01, zdepth=0.005, focus_xw=3.6, focus_yh=3.6, target_index=1)
   AT (0,0,0.2) RELATIVE coll2
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT detector = PSD_monitor(
    nx = 128, ny = 128, filename = "PSD.dat", xmin = -0.3,
    xmax = 0.3, ymin = -0.3, ymax = 0.3)
  AT (0, 0, 3) RELATIVE sample

COMPONENT Ldetector = L_monitor(
    nL = 1000, filename = "Edet.dat", xmin = -0.3,
    xmax = 0.3, ymin = -0.3, ymax = 0.3, Lmin = 0.5,
    Lmax = 2.5)
  AT (0, 0, 3.01) RELATIVE sample

COMPONENT PSDrad = PSD_monitor(
        filename = "psd2.dat",  radius = 0.3)
  AT (0, 0, 3.02) RELATIVE sample

END

